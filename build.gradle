// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        versions = [
                'minSdk'                : 14, // 最低版本
                'compileSdk'            : 27, // 编译版本
                'targetSdk'             : 27, // 目标版本

                'kotlin'                : '1.3.11', // Kotlin版本
                'androidPlugin'         : '3.3.0', // Android Gradle版本
                'buildTools'            : '28.0.3', // 编译工具版本
                'supportLibrary'        : '28.0.0', // Android Support包版本

                'majorNumber'           : 1, // 大版本号
                'minorNumber'           : 4, // 小版本号
                'revisionNumberCMD'     : 'git rev-list HEAD --count', // 提交次数命令
                'revisionDescriptionCMD': 'git describe --always', // 提交记录命令
        ]

        deps = [
                test   : [
                        'junit': "junit:junit:4.12"
                ],
                kotlin : [
                        'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
                ],
                android: [
                        'gradlePlugin'            : "com.android.tools.build:gradle:${versions.androidPlugin}",
                        'gradleBintrayPlugin'     : 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3',
                        'androidMavenGradlePlugin': 'com.github.dcendents:android-maven-gradle-plugin:1.5'
                ],
                support: [
                        'test'        : [
                                'runner'               : 'com.android.support.test:runner:1.0.2',
                                'espresso'             : "com.android.support.test.espresso:espresso-core:3.0.2",
                                'instrumentationRunner': "android.support.test.runner.AndroidJUnitRunner"
                        ],
                        'kotlin'      : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
                        'appcompat'   : "com.android.support:support-compat:${versions.supportLibrary}",
                        'design'      : "com.android.support:design:${versions.supportLibrary}",
                        'recyclerview': "com.android.support:recyclerview-v7:${versions.supportLibrary}",
                        'constraint'  : 'com.android.support.constraint:constraint-layout:1.1.3'
                ],
                bintray: [
                        'gradlePlugin': 'com.novoda:bintray-release:0.8.1'
                ],
                'gson' : 'com.google.code.gson:gson:2.8.5'
        ]

        // 获取 git 提交次数
        try {
            revisionNumber = versions.revisionNumberCMD.execute().getText().trim().toInteger()
        } catch (Exception ignore) {
            revisionNumber = 1
        }
        // 获取最后一次 git 记录码
        try {
            revisionDescription = versions.revisionDescriptionCMD.execute().getText().trim()
        } catch (Exception ignore) {
            revisionDescription = "1"
        }
        // 版本号
        verCode = versions.majorNumber * 1000000 + versions.minorNumber * 10000 + revisionNumber
        // 版本名(大版本.小版本.提交次数.提交记录码)
        verName = versions.majorNumber + "." + versions.minorNumber + "." + revisionNumber + "." + revisionDescription
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath deps.android.gradlePlugin
        classpath deps.kotlin.gradlePlugin
        classpath deps.bintray.gradlePlugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

// 指定javadoc格式
task javadoc(type: Javadoc) {
    options.encoding = "UTF-8"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
